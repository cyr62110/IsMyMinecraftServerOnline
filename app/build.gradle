buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.13.+'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'android-apt'

ext {
    androidAnnotationsVersion = '3.1'
    androidToolsVersion = '21.0.1'
    supportLibraryVersion = '21.0.+'
}

dependencies {
    //Android Annotations to simplify coding using annotations to inject views, etc...
    apt "org.androidannotations:androidannotations:$androidAnnotationsVersion"
    compile "org.androidannotations:androidannotations-api:$androidAnnotationsVersion"

    //ORMLite to simplify the use of the sqlite database
    compile "com.j256.ormlite:ormlite-android:4.48"

    //RxAndroid to use reactive functional programming for network operations
    compile "com.netflix.rxjava:rxjava-android:0.20.6"

    //Support v4 to use big view notification
    compile "com.android.support:support-v4:$supportLibraryVersion"

    //AboutMyApp library
    compile project(':aboutmyapp')

    //Minecraft Tools libraries
    compile project(':minecraftToolsFor1.7')
    compile project(':minecraftToolsFor1.8')
}

apt {
    arguments {
        androidManifestFile variant.processResources.manifestFile
        resourcePackageName 'fr.cvlaminck.immso'
    }
}

android {
    compileSdkVersion 21
    buildToolsVersion '21.0.1'

    defaultConfig {
        applicationId "fr.cvlaminck.immso"
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 2
        versionName "1.0-alpha-2"
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        playstore_alpha {
            debuggable false
            jniDebugBuild true
            runProguard true
            zipAlign true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}


